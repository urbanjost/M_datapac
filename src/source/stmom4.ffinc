!*==stmom4.f90  processed by SPAG 7.51RB at 12:54 on 18 Mar 2022
      SUBROUTINE STMOM4(X,N,Iwrite,Xsmom4)
      IMPLICIT NONE
!*--STMOM428629
!*** Start of declarations inserted by SPAG
      REAL an , hold , sum , sum2 , sum4 , vb , X , xmean , Xsmom4
      INTEGER i , ipr , Iwrite , N
!*** End of declarations inserted by SPAG
!CCCC FOLLOWING LINE ADDED TO MAKE THIS A DLL.
!      DLL_EXPORT STMOM4
!
!     PURPOSE--THIS SUBROUTINE COMPUTES THE
!              SAMPLE STANDARDIZED FOURTH CENTRAL MOMENT
!              OF THE DATA IN THE INPUT VECTOR X.
!              THE SAMPLE STANDARDIZED FOURTH CENTRAL MOMENT =
!              (THE SAMPLE FOURTH CENTRAL MOMENT)/((THE SAMPLE
!              STANDARD DEVIATION)**4).
!              N (RATHER THAN N-1) HAS BEEN USED IN THE DENOMINATOR
!              IN THE CALCULATION OF BOTH THE SAMPLE FOURTH CENTRAL
!              MOMENT AND THE SAMPLE STANDARD DEVIATION.
!     INPUT  ARGUMENTS--X      = THE SINGLE PRECISION VECTOR OF
!                                (UNSORTED OR SORTED) OBSERVATIONS.
!                     --N      = THE INTEGER NUMBER OF OBSERVATIONS
!                                IN THE VECTOR X.
!                     --IWRITE = AN INTEGER FLAG CODE WHICH
!                                (IF SET TO 0) WILL SUPPRESS
!                                THE PRINTING OF THE
!                                SAMPLE STANDARDIZED FOURTH CENTRAL
!                                MOMENT AS IT IS COMPUTED;
!                                OR (IF SET TO SOME INTEGER
!                                VALUE NOT EQUAL TO 0),
!                                LIKE, SAY, 1) WILL CAUSE
!                                THE PRINTING OF THE
!                                SAMPLE STANDARDIZED FOURTH CENTRAL
!                                MOMENT AT THE TIME IT IS COMPUTED.
!     OUTPUT ARGUMENTS--XSMOM4 = THE SINGLE PRECISION VALUE OF THE
!                                COMPUTED SAMPLE STANDARDIZED FOURTH
!                                CENTRAL MOMENT.
!     OUTPUT--THE COMPUTED SINGLE PRECISION VALUE OF THE
!             SAMPLE STANDARDIZED FOURTH CENTRAL MOMENT.
!     PRINTING--NONE, UNLESS IWRITE HAS BEEN SET TO A NON-ZERO
!               INTEGER, OR UNLESS AN INPUT ARGUMENT ERROR
!               CONDITION EXISTS.
!     RESTRICTIONS--THERE IS NO RESTRICTION ON THE MAXIMUM VALUE
!                   OF N FOR THIS SUBROUTINE.
!     OTHER DATAPAC   SUBROUTINES NEEDED--NONE.
!     FORTRAN LIBRARY SUBROUTINES NEEDED--NONE.
!     MODE OF INTERNAL OPERATIONS--SINGLE PRECISION.
!     LANGUAGE--ANSI FORTRAN.
!     REFERENCES--KENDALL AND STUART, THE ADVANCED THEORY OF
!                 STATISTICS, VOLUME 1, EDITION 2, 1963, PAGES 85, 243.
!               --SNEDECOR AND COCHRAN, STATISTICAL METHODS,
!                 EDITION 6, 1967, PAGES 86-90.
!     WRITTEN BY--JAMES J. FILLIBEN
!                 STATISTICAL ENGINEERING LABORATORY (205.03)
!                 NATIONAL BUREAU OF STANDARDS
!                 WASHINGTON, D. C. 20234
!                 PHONE:  301-921-2315
!     ORIGINAL VERSION--JUNE      1972.
!     UPDATED         --SEPTEMBER 1975.
!     UPDATED         --NOVEMBER  1975.
!
!---------------------------------------------------------------------
!
      DIMENSION X(:)
!
      ipr = 6
!
!     CHECK THE INPUT ARGUMENTS FOR ERRORS
!
      an = N
      IF ( N<1 ) THEN
         WRITE (ipr,99001)
99001    FORMAT (' ',                                                   &
     &'***** FATAL ERROR--THE SECOND INPUT ARGUMENT TO THE STMOM4 SUBROU&
     &TINE IS NON-POSITIVE *****')
         WRITE (ipr,99002) N
99002    FORMAT (' ','***** THE VALUE OF THE ARGUMENT IS ',I8,' *****')
         RETURN
      ELSE
         IF ( N==1 ) THEN
            WRITE (ipr,99003)
99003       FORMAT (' ',                                                &
     &'***** NON-FATAL DIAGNOSTIC--THE SECOND INPUT ARGUMENT TO THE STMO&
     &M4 SUBROUTINE HAS THE VALUE 1 *****')
            Xsmom4 = 0.0
         ELSE
            hold = X(1)
            DO i = 2 , N
               IF ( X(i)/=hold ) GOTO 50
            ENDDO
            WRITE (ipr,99004) hold
99004       FORMAT (' ',                                                &
     &'***** NON-FATAL DIAGNOSTIC--THE FIRST  INPUT ARGUMENT (A VECTOR) &
     &TO THE STMOM4 SUBROUTINE HAS ALL ELEMENTS = ',E15.8,' *****')
            Xsmom4 = 0.0
         ENDIF
         GOTO 100
!
!-----START POINT-----------------------------------------------------
!
 50      sum = 0.0
         DO i = 1 , N
            sum = sum + X(i)
         ENDDO
         xmean = sum/an
         sum2 = 0.0
         sum4 = 0.0
         DO i = 1 , N
            sum2 = sum2 + (X(i)-xmean)**2
            sum4 = sum4 + (X(i)-xmean)**4
         ENDDO
         vb = sum2/an
         sum4 = sum4/an
         Xsmom4 = sum4/(vb*vb)
      ENDIF
!
 100  IF ( Iwrite==0 ) RETURN
      WRITE (ipr,99005)
99005 FORMAT (' ')
      WRITE (ipr,99006) N , Xsmom4
99006 FORMAT (' ',                                                      &
     &        'THE SAMPLE STANDARDIZED FOURTH CENTRAL MOMENT FOR THE ', &
     &        I6,' OBSERVATIONS IS ',E15.8)
      END SUBROUTINE STMOM4
