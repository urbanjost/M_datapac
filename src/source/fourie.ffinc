$!@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
$BLOCK COMMENT --file fourie.3m_datapac.man
NAME
   fourie(3f) - [M_datapac:ANALYSIS] perform a Fourier analysis of a data set

SYNOPSIS
      SUBROUTINE FOURIE(X,N)

       REAL(kind=wp),intent(in) :: X(:)
       INTEGER                  :: N

DESCRIPTION
   FOURIE(3f) performs a Fourier analysis of the data in the input vector
   X. The analysis consists of the following--

      1. computing (and printing)
         (for each of the harmonic frequencies
         1/n, 2/n, 3/n, ..., 1/2)
         the corresponding fourier coefficients,
         the amplitude, the phase,
         the contribution to the total variance,
         and the relative contribution to the total
         variance.
      2. plotting out a fourier line spectrum =
         the periodogram = the plot of relative
         contribution to total variance
         (at each fourier frequency) versus
         the fourier frequency.

   In order that the results of the Fourier analysis be valid and properly
   interpreted, the input data in X should be equi-spaced in time (or
   whatever variable corresponds to time).

   The horizontal axis of the spectra produced by fourie(3f) is frequency.
   This frequency is measured in units of cycles per 'data point' or,
   more precisely, in cycles per unit time where 'unit time' is defined
   as the elapsed time between adjacent observations.

   The range of the frequency axis is 0.0 to 0.5.

   Fourier analysis differs from spectral analysis (as, for example,
   produced by the datapac TIMESE(3f) subroutine) in that a Fourier
   analysis does no smoothing on the spectral estimates whereas a spectral
   analysis does smooth the spectral estimates. The net result is that
   the spectral estimates obtained from a Fourier analysis are almost
   always more variable than those obtained in a spectral analysis.

   The practical conclusion is that when the data analyst has a choice
   of whether to perform a Fourier analysis or a spectral analysis,
   the spectral analysis should almost always be preferred.

   the maximum number of Fourier frequencies for which the Fourier
   coefficients is computed (and listed) is N/2 where N is the sample
   size (length of the data record in the vector X). This rule is
   overridden (for listing purposes only) in large data sets and is
   replaced by the rule that the maximum number of lags listed = 800
   (which corresponds to an 8-page listing of Fourier coefficients.
   If more pages are desired, change the value of the variable MAXPAG
   within this subroutine from 8 to whatever is desired.

   If the input observations in X are considered to have been collected
   1 second apart in time, then the frequency axis of the resulting
   spectra would be in units of Hertz (= cycles per second).

   The frequency of 0.0 corresponds to a cycle in the data of infinite
   (= 1/(0.0)) length or period. the frequency of 0.5 corresponds to
   a cycle in the data of length = 1/(0.5) = 2 data points.

   Any equi-spaced fourier analysis is intrinsically limited to detecting
   frequencies no larger than 0.5 cycles per data point; this corresponds
   to the fact that the smallest detectable cycle in the data is 2 data
   points per cycle.

INPUT ARGUMENTS

   X   The vector of (unsorted) observations.

   N   The integer number of observations in the vector X.
       The maximum allowable value of N for this subroutine is 15000.
       The sample size N must be greater than or equal to 3.

OUTPUT

   2 to 10 pages (depending on the input sample size) of automatic
   printout--

     1. a listing of the amplitude, phase, contribution to the total
        variance, and relative contribution to the total variance for
        each of the fourier frequencies (1/n, 2/n, 3/n, ..., 1/2).
        this listing may take as little as 1 page or as many as n/100
        pages (the exact number depending on the input sample size n).
        this listing is terminated after at most 8 computer pages.
        if more pages are desired, change the value of the variable
        maxpag within this subroutine from 8 to whatever desired.

     2. a plot of the relative contribution to the total variance versus
        frequency.

EXAMPLES
  Sample program:

   program demo_fourie
   use M_datapac, only : fourie
   implicit none
   ! call fourie(x,y)
   end program demo_fourie

  Results:

AUTHOR
   The original DATAPAC library was written by James Filliben of the
   Statistical Engineering Division, National Institute of Standards
   and Technology.

MAINTAINER
   John Urban, 2022.05.31

LICENSE
   CC0-1.0

REFERENCES
  * Jenkins and Watts, especially Page 290.

$ENDBLOCK
!     ORIGINAL VERSION--NOVEMBER  1972.
!     UPDATED         --NOVEMBER  1975.
!     UPDATED         --FEBRUARY  1976.
$!@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
! processed by SPAG 7.51RB at 12:54 on 18 Mar 2022
SUBROUTINE FOURIE(X,N)
REAL(kind=wp),intent(in) :: X(:)
INTEGER                  :: N
REAL(kind=wp)    :: ai, amp, an, angdeg, angrad, conmsq, del, ffreq, hold, percon, period, phase1, phase2, pi, sum, suma, sumb, t
REAL(kind=wp)    :: A(7500), B(7500)
REAL(kind=wp)    :: vbias, WS, xbar
INTEGER          :: i, ievodd, ilower, ipage, iskip, iupper, j, maxpag, nhalf, nnpage
CHARACTER(len=4) :: alperc
COMMON /BLOCK2_${KIND}/ WS(15000)
EQUIVALENCE (A(1),WS(1))
EQUIVALENCE (B(1),WS(7501))
DATA pi/3.14159265358979_wp/
DATA alperc/'%'/
!
      ilower = 3
      iupper = 15000
      maxpag = 8
!
!     CHECK THE INPUT ARGUMENTS FOR ERRORS
!
      IF ( N<ilower .OR. N>iupper ) THEN
         WRITE (G_IO,99001) ilower , iupper
99001    FORMAT (' ',                                                   &
     &'***** FATAL ERROR--THE SECOND INPUT ARGUMENT TO THE FOURIE SUBROU&
     &TINE IS OUTSIDE THE ALLOWABLE (',I0,',',I0,') INTERVAL *****')
         WRITE (G_IO,99002) N
99002    FORMAT (' ','***** THE VALUE OF THE ARGUMENT IS ',I0,' *****')
         RETURN
      ELSE
         hold = X(1)
         DO i = 2 , N
            IF ( X(i)/=hold ) GOTO 100
         ENDDO
         WRITE (G_IO,99003) hold
99003    FORMAT (' ',                                                   &
     &'***** NON-FATAL DIAGNOSTIC--THE FIRST  INPUT ARGUMENT (A VECTOR) &
     &TO THE FOURIE SUBROUTINE HAS ALL ELEMENTS = ',E15.8,' *****')
         RETURN
      ENDIF
!
!-----START POINT-----------------------------------------------------
!
 100  an = N
!
!     DETERMINE IF N IS ODD OR EVEN
!
      ievodd = N - 2*(N/2)
      del = (an+1.0_wp)/2.0_wp
      IF ( ievodd==0 ) del = (an+2.0_wp)/2.0_wp
!
!     COMPUTE THE SAMPLE MEAN
!
      sum = 0.0_wp
      DO i = 1 , N
         sum = sum + X(i)
      ENDDO
      xbar = sum/an
!
!     COMPUTE THE BIASED SAMPLE VARIANCE
!
      sum = 0.0_wp
      DO i = 1 , N
         sum = sum + (X(i)-xbar)**2
      ENDDO
      vbias = sum/an
!
!     COMPUTE THE FOURIER COSINE AND SINE COEFFICIENTS--THEY ARE PLACED
!     IN VECTORS A AND B, RESPECTIVELY.
!
      nhalf = N/2
      DO i = 1 , nhalf
         ai = i
         suma = 0.0_wp
         sumb = 0.0_wp
         DO j = 1 , N
            t = j
            suma = suma + X(j)*COS(2.0_wp*pi*(ai/an)*(t-del))
            sumb = sumb + X(j)*SIN(2.0_wp*pi*(ai/an)*(t-del))
         ENDDO
         A(i) = suma/an
         B(i) = sumb/an
      ENDDO
!
!     WRITE OUT THE SAMPLE SIZE, THE SAMPLE MEAN,
!     AND THE (BIASED) SAMPLE VARIANCE.
!
      WRITE (G_IO,99013)
      WRITE (G_IO,99004)
!
99004 FORMAT (' ',44X,'FOURIER ANALYSIS')
      WRITE (G_IO,99014)
      WRITE (G_IO,99014)
      WRITE (G_IO,99005) N
99005 FORMAT (' ',40X,'THE SAMPLE SIZE N                      = ',I0)
      WRITE (G_IO,99006) xbar
99006 FORMAT (' ',40X,'THE SAMPLE MEAN                        = ',F20.8)
      WRITE (G_IO,99007) vbias
99007 FORMAT (' ',40X,'THE SAMPLE VARIANCE (WITH DIVISOR N-1) = ',F20.8)
      WRITE (G_IO,99014)
!
!     COMPUTE THE HARMONIC CONTRIBUTION
!     AT EACH OF THE FOURIER FREQUENCIES.
!     THE FUNDAMENTAL FOURIER FREQUENCY
!     IS 1/N CYCLES PER DATA POINT
!     (WHERE N = THE INPUT SAMPLE SIZE).
!     THE OTHER FOURIER FREQUENCIES
!     ARE MULTIPLES OR HARMONICS
!     (2/N, 3/N, 4/N, ...1/2) OF THE FUNDAMENTAL.
!     COMPUTE AMPLITUDES, PHASES, AND
!     CONTRIBUTIONS TO THE VARIANCE AT EACH
!     OF THE FOURIER FREQUENCIES.
!     COMPUTE THE PERCENTAGE CONTRIBUTION
!     TO THE TOTAL VARIANCE AT EACH
!     OF THE FOURIER FREQUENCIES.
!     NOTE--TO SAVE STORAGE, ALSO COPY
!     THE PERCENTAGE CONTRIBUTIONS TO THE VARIANCE)
!     (WHICH WILL LATER BE PLOTTED OUT LIKE A SPECTRUM)
!     INTO THE VECTOR A; THIS WILL DESTROY
!     THE PREVIOUS CONTENTS OF THE VECTOR A.
!     WRITE OUT ALL OF THE ABOVE.
!
      nnpage = 50
      i = 0
      DO ipage = 1 , maxpag
         WRITE (G_IO,99013)
         WRITE (G_IO,99008)
99008    FORMAT (' ','     I   FOURIER   PERIOD      FOURIER  ',        &
     &           '      FOURIER       AMPLITUDE ',                      &
     &           '      PHASE          PHASE         VARIANCE   ',      &
     &           '  RELATIVE')
         WRITE (G_IO,99009)
99009    FORMAT (' ','        FREQUENCY            COEFFICIENT    ',    &
     &           'COEFFICIENT',                                         &
     &     '                    RADIANS        DEGREES        COMPONENT'&
     &     ,'    VARIANCE')
         WRITE (G_IO,99010)
99010    FORMAT (' ','     (CYCLES/POINT)             A(I)       ',     &
     &           '    B(I)      ',                                      &
     &           '                                                  ',  &
     &           '         COMPONENT (%)')
         DO j = 1 , nnpage
            i = i + 1
            ai = i
            ffreq = ai/an
            period = 1.0_wp/ffreq
            angrad = (ai/an)*2.0_wp*pi
            angdeg = (ai/an)*360.0_wp
            amp = SQRT(A(i)*A(i)+B(i)*B(i))
            phase1 = ATAN(-B(i)/A(i))
            phase2 = phase1*360.0_wp/(2.0_wp*pi)
            conmsq = 2.0_wp*amp*amp
            IF ( i==nhalf .AND. ievodd==0 ) conmsq = conmsq/2.0_wp
            percon = 100.0_wp*(conmsq/vbias)
            WRITE (G_IO,99011) i , ffreq , period , A(i) , B(i) , amp ,  &
     &                        phase1 , phase2 , conmsq , percon , alperc
99011       FORMAT (' ',I0,2X,F8.6,1X,F8.2,6(1X,E14.7),2X,F6.2,A1)
            A(i) = percon
            IF ( i>=nhalf ) GOTO 200
            iskip = i - 10*(i/10)
            IF ( iskip==0 ) WRITE (G_IO,99014)
         ENDDO
      ENDDO
!
!     PLOT OUT THE PERCENTAGE CONTRIBUTIONS
!     TO THE TOTAL VARIANCE AT
!     EACH OF THE FOURIER FREQUENCIES
!     (1/N, 2/N, 3/N, ..., 1/2).
!     THIS WILL CORRESPOND TO A SPECTRAL
!     PLOT IN SPECTRAL ANALYSIS.
!
 200  CALL PLOTSP(A,nhalf,0)
      WRITE (G_IO,99012)
99012 FORMAT (' ',40X,                                                  &
     &        'PERIODOGRAM = FOURIER LINE SPECTRUM OF THE ORIGINAL DATA'&
     &        )
99013 FORMAT ('1')
99014 FORMAT (' ')
!
END SUBROUTINE FOURIE
