!*==relsd.f90  processed by SPAG 7.51RB at 12:54 on 18 Mar 2022
      SUBROUTINE RELSD(X,N,Iwrite,Xrelsd)
      IMPLICIT NONE
!*--RELSD26115
!*** Start of declarations inserted by SPAG
      REAL an , hold , sd , sum , var , X , xmean , Xrelsd
      INTEGER i , ipr , Iwrite , N
!*** End of declarations inserted by SPAG
!CCCC FOLLOWING LINE ADDED TO MAKE THIS A DLL.
!      DLL_EXPORT RELSD
!
!     PURPOSE--THIS SUBROUTINE COMPUTES THE
!              SAMPLE RELATIVE STANDARD DEVIATION
!              OF THE DATA IN THE INPUT VECTOR X.
!              THE SAMPLE RELATIVE STANDARD DEVIATION = (THE SAMPLE
!              STANDARD DEVIATION)/(THE SAMPLE MEAN).
!              THE DENOMINATOR N-1 IS USED IN COMPUTING THE
!              SAMPLE STANDARD DEVIATION.
!              THE SAMPLE RELATIVE STANDARD DEVIATION IS ALTERNATIVELY
!              REFERRED TO AS THE SAMPLE COEFFICIENT OF VARIATION.
!     INPUT  ARGUMENTS--X      = THE SINGLE PRECISION VECTOR OF
!                                (UNSORTED OR SORTED) OBSERVATIONS.
!                     --N      = THE INTEGER NUMBER OF OBSERVATIONS
!                                IN THE VECTOR X.
!                     --IWRITE = AN INTEGER FLAG CODE WHICH
!                                (IF SET TO 0) WILL SUPPRESS
!                                THE PRINTING OF THE
!                                SAMPLE RELATIVE STANDARD DEVIATION
!                                AS IT IS COMPUTED;
!                                OR (IF SET TO SOME INTEGER
!                                VALUE NOT EQUAL TO 0),
!                                LIKE, SAY, 1) WILL CAUSE
!                                THE PRINTING OF THE
!                                SAMPLE RELATIVE STANDARD DEVIATION
!                                AT THE TIME IT IS COMPUTED.
!     OUTPUT ARGUMENTS--XRELSD = THE SINGLE PRECISION VALUE OF THE
!                                COMPUTED SAMPLE RELATIVE
!                                STANDARD DEVIATION.
!     OUTPUT--THE COMPUTED SINGLE PRECISION VALUE OF THE
!             SAMPLE RELATIVE STANDARD DEVIATION.
!     PRINTING--NONE, UNLESS IWRITE HAS BEEN SET TO A NON-ZERO
!               INTEGER, OR UNLESS AN INPUT ARGUMENT ERROR
!               CONDITION EXISTS.
!     RESTRICTIONS--THERE IS NO RESTRICTION ON THE MAXIMUM VALUE
!                   OF N FOR THIS SUBROUTINE.
!     OTHER DATAPAC   SUBROUTINES NEEDED--NONE.
!     FORTRAN LIBRARY SUBROUTINES NEEDED--SQRT.
!     MODE OF INTERNAL OPERATIONS--SINGLE PRECISION.
!     LANGUAGE--ANSI FORTRAN.
!     REFERENCES--KENDALL AND STUART, THE ADVANCED THEORY OF
!                 STATISTICS, VOLUME 1, EDITION 2, 1963, PAGES 47, 233.
!               --SNEDECOR AND COCHRAN, STATISTICAL METHODS,
!                 EDITION 6, 1967, PAGES 62-65.
!     WRITTEN BY--JAMES J. FILLIBEN
!                 STATISTICAL ENGINEERING LABORATORY (205.03)
!                 NATIONAL BUREAU OF STANDARDS
!                 WASHINGTON, D. C. 20234
!                 PHONE:  301-921-2315
!     ORIGINAL VERSION--JUNE      1972.
!     UPDATED         --MARCH     1975.
!     UPDATED         --SEPTEMBER 1975.
!     UPDATED         --NOVEMBER  1975.
!
!---------------------------------------------------------------------
!
      DIMENSION X(:)
!
      ipr = 6
!
!     CHECK THE INPUT ARGUMENTS FOR ERRORS
!
      an = N
      IF ( N<1 ) THEN
         WRITE (ipr,99001)
99001    FORMAT (' ',                                                   &
     &'***** FATAL ERROR--THE SECOND INPUT ARGUMENT TO THE RELSD  SUBROU&
     &TINE IS NON-POSITIVE *****')
         WRITE (ipr,99002) N
99002    FORMAT (' ','***** THE VALUE OF THE ARGUMENT IS ',I8,' *****')
         RETURN
      ELSE
         IF ( N==1 ) THEN
            WRITE (ipr,99003)
99003       FORMAT (' ',                                                &
     &'***** NON-FATAL DIAGNOSTIC--THE SECOND INPUT ARGUMENT TO THE RELS&
     &D  SUBROUTINE HAS THE VALUE 1 *****')
            Xrelsd = 0.0
         ELSE
            hold = X(1)
            DO i = 2 , N
               IF ( X(i)/=hold ) GOTO 50
            ENDDO
            WRITE (ipr,99004) hold
99004       FORMAT (' ',                                                &
     &'***** NON-FATAL DIAGNOSTIC--THE FIRST  INPUT ARGUMENT (A VECTOR) &
     &TO THE RELSD  SUBROUTINE HAS ALL ELEMENTS = ',E15.8,' *****')
            Xrelsd = 0.0
         ENDIF
         GOTO 100
!
!-----START POINT-----------------------------------------------------
!
 50      sum = 0.0
         DO i = 1 , N
            sum = sum + X(i)
         ENDDO
         xmean = sum/an
         sum = 0.0
         DO i = 1 , N
            sum = sum + (X(i)-xmean)**2
         ENDDO
         var = sum/(an-1.0)
         sd = SQRT(var)
         Xrelsd = 100.0*sd/xmean
      ENDIF
!
 100  IF ( Iwrite==0 ) RETURN
      WRITE (ipr,99005)
99005 FORMAT (' ')
      WRITE (ipr,99006) N , Xrelsd
99006 FORMAT (' THE RELATIVE STANDARD DEVIATION (= STANDARD ',          &
     &        'DEVIATION/MEAN) FOR THE ',I6,' OBSERVATIONS IS ',E12.8,  &
     &        ' PERCENT')
      END SUBROUTINE RELSD
